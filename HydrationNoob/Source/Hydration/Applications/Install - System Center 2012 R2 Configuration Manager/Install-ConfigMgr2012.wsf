<job id="Install-ConfigMgr2012">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

'//----------------------------------------------------------------------------
'// Solution: Hydration
'// Purpose: Used to install ConfigMgr 2012 R2
'// Usage: cscript Install-ConfigMgr2012.wsf [/debug:true]
'// Version: 1.2 - 18 October 2013 - Johan Arwidmark
'//
'// This script is provided "AS IS" with no warranties, confers no rights and 
'// is not supported by the authors or Deployment Artist. 
'//
'//----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declaration
'//---------------------------------------------------------------------------- 

Option Explicit 

Dim iRetVal 

'//----------------------------------------------------------------------------
'// End declarations
'//---------------------------------------------------------------------------- 

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 

'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'//
'// Function: ZTIProcess()
'//
'// Input: None
'// 
'// Return: Success - 0
'// Failure - non-zero
'//
'// Purpose: Perform main ZTI processing
'// 
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sFile
	Dim sCmd
	
	' Install ConfigMgr 2012 R2 Primary site

	oLogging.ReportProgress "Installing ConfigMgr 2012 R2", 20

	sFile = oUtility.ScriptDir & "\Source\SMSSETUP\BIN\X64\setup.exe"
	If not oFSO.FileExists(sFile) then
		oLogging.CreateEntry "Install ConfigMgr 2012 R2: " & sFile & " was not found, unable to install ConfigMgr", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if

	sCmd = """" & oUtility.ScriptDir & "\Source\SMSSETUP\BIN\X64\setup.exe""" & " /Script """ & oUtility.ScriptDir & "\ConfigMgr2012Unattend.ini""" & " /NoUserInput"
	iRetVal = oUtility.RunWithHeartbeat(sCmd)

	if iRetVal <> 0 then
		oLogging.CreateEntry "Install ConfigMgr 2012 R2: Error installing ConfigMgr, see C:\ConfigMgrSetup.log for details.", LogTypeWarning
	End If
	
	' Verify Install ConfigMgr 2012 R2
	if VerifyConfigMgrInstall > 0 then 
		oLogging.CreateEntry "Install ConfigMgr 2012 R2: ConfigMgr Installed successfully", LogTypeInfo
		ZTIProcess = Success
	else
		oLogging.CreateEntry "Install ConfigMgr 2012 R2: Unable to verify ConfigMgr Installed successfully", LogTypeWarning
		ZTIProcess = Failure
	End if
	Err.Clear

End Function 


Function VerifyConfigMgrInstall()

	Const ForReading = 1

	Dim objRegEx, objFSO
	Dim objFile, strSearchString, colMatches
	
	Set objRegEx = CreateObject("VBScript.RegExp")
	objRegEx.Global = True   
	objRegEx.Pattern = "Completed Configuration Manager 2012 R2 Server Setup"

	Set objFSO = CreateObject("Scripting.FileSystemObject")
	Set objFile = objFSO.OpenTextFile("C:\ConfigMgrSetup.log", ForReading)
	strSearchString = objFile.ReadAll
	objFile.Close

	Set colMatches = objRegEx.Execute(strSearchString)  

	VerifyConfigMgrInstall = colMatches.Count

End Function

</script>
</job>